group 'net.evendanan'

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

apply from: "${rootDir}/gradle/android_general.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    defaultConfig {
        applicationId 'com.menny.android.anysoftkeyboard'
        versionCode rootProject.autoVersioning.versionData.versionCode
        //versionName rootProject.autoVersioning.versionData.versionName
        versionName "1.10.1572"
        archivesBaseName = "AnySoftKeyboard-v$versionName"

        println "Building AnySoftKeyboard ${versionName} (or ${project.version}), code ${versionCode}."

        //adding additional fields to the BuildConfig class.
        String support_email_address = System.getenv("ANYSOFTKEYBOARD_CRASH_REPORT_EMAIL")
        println 'crash report email is: ' + support_email_address

        buildConfigField "String", "CRASH_REPORT_EMAIL_ADDRESS", '"' + support_email_address + '"'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            v1SigningEnabled true
            v2SigningEnabled false
            v2SigningEnabled false
            println "Using '${storeFile}' to release APK (with alias '${keyAlias}')."
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false

            minifyEnabled true
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.txt', 'proguard-anysoftkeyboard.txt'
        }

        debug {
            signingConfig signingConfigs.release
            testCoverageEnabled true
            minifyEnabled false
        }

        canary {
            minifyEnabled true
            signingConfig signingConfigs.release
            initWith buildTypes.release

            matchingFallbacks = ['release', 'debug']
        }
    }
}

//adding TESTING_BUILD
android {
    buildTypes {
        release {
            buildConfigField("boolean", "TESTING_BUILD", "false")
        }

        debug {
            buildConfigField("boolean", "TESTING_BUILD", "true")
        }

        canary {
            buildConfigField("boolean", "TESTING_BUILD", "true")
        }
    }
}

play {
    track = System.getProperty('deployChannel', 'alpha')
    serviceAccountEmail = System.getenv().getOrDefault('PUBLISH_APK_SERVICE_ACCOUNT_EMAIL', 'dummy@example.com')
    serviceAccountCredentials = file('/tmp/apk_upload_key.p12')
}

//verifying release-notes file
File playStoreWhatsNewFile = file("${project.projectDir}/src/main/play/release-notes/en-US/${System.getProperty('deployChannel', 'alpha')}.txt")
if (!playStoreWhatsNewFile.exists()) {
    throw new FileNotFoundException("Can not find whatsnew file for Play-Store upload!")
}

if (playStoreWhatsNewFile.text.length() > 500) {
    println("Locale " + Locale.getDefault())
    println("file encoding " + CharsetToolkit.defaultSystemCharset)
    println("File contents:")
    println("***" +  playStoreWhatsNewFile.text + "***")
    throw new IllegalStateException("whatsnew file can not be longer than 500 characters! Currently " + playStoreWhatsNewFile.text.length())
}

dependencies {
    implementation "com.anysoftkeyboard.language.english:english:$askExternalAddOnsVersion"
    implementation "com.anysoftkeyboard:base:$askExternalAddOnsVersion"
    implementation project(':base')
    implementation project(':base-rx')
    implementation project(':nextword')
    implementation project(':dictionaries')
    implementation project(':jnidictionaryv1')
    implementation project(':jnidictionaryv2')
    implementation project(':prefs')
    implementation project(':overlay')
    implementation project(':pixel')
    implementation project(':remote')

    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation 'com.jpardogo.materialtabstrip:library:1.1.0'
    implementation 'com.github.karczews:rx2-broadcast-receiver:1.0.5'
    implementation('com.github.menny.Chauffeur:permissions:0.1.1') {
        exclude group: 'com.android.support'
    }

    testImplementation project(path: ':base-test')
    testImplementation 'com.github.triplet.simpleprovider:simpleprovider:1.1.0'
}
